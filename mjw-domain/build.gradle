apply from: 'databaseSetup.gradle'

repositories {
    mavenCentral()
}

dependencies {
    compile project(':mjw-common')
    runtime 'com.h2database:h2:1.3.170'
}

sourceSets {
    integrationTest {
        java.srcDir file('src/itest/java')
        resources.srcDir file('src/itest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

startDatabase.dependsOn test
integrationTest.dependsOn startAndPrepareDatabase
stopDatabase.dependsOn integrationTest
check.dependsOn stopDatabase

/*
sonarRunner {
    sonarProperties {
        properties['sonar.tests'] += sourceSets.integrationTest.allSource.srcDirs
    }
}
*/
